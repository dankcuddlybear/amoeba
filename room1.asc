// room script file
bool say_once = false;
function room_FirstLoad() {}
function room_Load() {cFishEatingSoup.SetIdleView(6, 0);}
function room_AfterFadeIn() {}

// [HOTSPOT FUNCTIONS]
function hOfficeDoor_AnyClick() {
  if (Verbs.UsedAction(eGA_LookAt)) {
    player.Think("Looks like this door leads to King Krab's office. But it's locked.");
  } else if (Verbs.UsedAction(eGA_Use) || Verbs.UsedAction(eGA_Open)) {
    player.Think("It's locked. I'll have to get in some other way.");
    player.Think("Perhaps I'll try to pole-jump my way through that window with that fork somebody left.");
  }
}
function hKitchenDoor_AnyClick() {
  if (Verbs.UsedAction(eGA_LookAt)) {
    player.Think("Looks like this door leads to the kitchen.");
  } else if(Verbs.UsedAction(eGA_Use) || Verbs.UsedAction(eGA_Open)) {
    player.Think("I'm too small to open it.");
    player.Think("Perhaps I'll try to pole-jump my way through that window with that fork somebody left.");
  }
}
function hTill_AnyClick() {
  if (Verbs.UsedAction(eGA_LookAt)) player.Think("Business isn't great. I could really use a bit of money.");
  else if (Verbs.UsedAction(eGA_Open) || Verbs.UsedAction(eGA_Use)) { // Steal money
    if (player.HasInventory(iMoney)) player.Think("There's no more money left.");
    else {player.Walk(160, 148, eBlock); player.AddInventory(iMoney); player.Say("MONEY!!!");}
  }
}
function hSoupBowl_Look() {player.Think("It's a bowl of putrid red soup.");}



// [OBJECT FUNCTIONS]
function oFork_PickUp() {player.Walk(oFork.X, oFork.Y, eBlock); oFork.Visible = false; player.AddInventory(iFork);}
function oFork_Look() {iFork.RunInteraction(eModeLookat);}
function oFork_Interact() {oFork.RunInteraction(eModePickup); iFork.RunInteraction(eModeInteract);} // Pick up and use

// Room scripts, called by CallRoomScript(int value)
function on_call(int value) {
  if (value == 0) { // Use fork to jump through window, but ends up in bowl of soup instead
    player.Think("OK here goes...");
    player.Walk(100, 154, eBlock);
    player.SetWalkSpeed(10, 1);
    player.Walk(200, player.y, eBlock);
    aBoingPlop.Play();
    player.LoseInventory(iFork);
    oForkStuck.Visible = true;
    player.ChangeView(4);
    player.Move(233, 135, eBlock, eAnywhere); player.x = 236; // Jump across border
    player.Move(260, 125, eBlock, eAnywhere);
    player.ChangeView(5);
    player.Move(292, 138, eBlock, eAnywhere);
    player.ChangeView(1);
    cFishEatingSoup.Animate(0, 20);
    cFishEatingSoup.LockView(7);
    player.x = 306; player.y = 126;
    player.ChangeView(4);
    player.Move(128, 86, eNoBlock, eAnywhere);
    for (int i = 0; i < 10; i++) {cFishEatingSoup.Animate(0, 1);}
    player.SetWalkSpeed(6, 4);
    player.ChangeRoom(2, 28, 78, eDirectionLeft);
  }
}
